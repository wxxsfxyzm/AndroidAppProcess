plugins {
    alias(libs.plugins.agp.lib)
    id "maven-publish"
    id "signing"
}

android {
    namespace = "com.rosan.app_process"
    compileSdk = TARGET_SDK

    defaultConfig {
        minSdk = MIN_SDK
        targetSdk = TARGET_SDK
    }

    buildFeatures {
        buildConfig = true
        aidl = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    compileOnly project(":hidden-api")
    compileOnly(libs.annotation)

    implementation(libs.commons.cli)
    implementation(libs.hiddenapibypass)
}

afterEvaluate {
    tasks.named('sourceReleaseJar', Jar) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

publishing {
    publications {
        mavenRelease(MavenPublication) {
            afterEvaluate {
                from components.findByName('release')
            }

            groupId = 'com.github.wxxsfxyzm'
            artifactId = 'app-process'
            version = ARTIFACT_VERSION

            pom {
                name = 'App Process Revived'
                description = 'My custom fork of App Process'
                url = "https://github.com/wxxsfxyzm/AndroidAppProcess"
            }
        }
    }
}